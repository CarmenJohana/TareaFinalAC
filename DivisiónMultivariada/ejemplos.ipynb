{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "23aac943-7d25-4dbf-ba2f-29f39fea217b",
   "metadata": {},
   "outputs": [],
   "source": [
    "R.<x,y> = PolynomialRing(RR, 2, \"xy\", order = \"lex\")\n",
    "\n",
    "def div_poly(F, f, R = R):\n",
    "    \"\"\"\n",
    "    Implementa el algoritmo de division\n",
    "    de polinomios multivariados.\n",
    "    De: Ideals, Varieties, and Algorithms, pag 65\n",
    "    F es una lista de polinomios, f es el polinomio a dividir\n",
    "    R es el conjunto de polinomios sobre el que se\n",
    "    \"\"\"\n",
    "    q = [R(0) for _ in range(len(F))]\n",
    "    r = R(0)\n",
    "    p = f\n",
    "\n",
    "    while p!=R(0):\n",
    "        i=0\n",
    "        divisionoccurred = False\n",
    "        while i<len(F) and not divisionoccurred:\n",
    "\n",
    "            if hasattr(R, \"monomial_quotient\"):\n",
    "                quo = R.monomial_quotient(p.lt(),F[i].lt(),coeff=True)\n",
    "                rem = p.lt().reduce(F[i].lt())\n",
    "            else:\n",
    "                quo, rem = p.lt().quo_rem(F[i].lt())\n",
    "            if rem == 0:\n",
    "                q[i] = q[i]+quo\n",
    "                p = p - quo*F[i]\n",
    "                divisionoccurred = True\n",
    "            else:\n",
    "                i += 1\n",
    "        if not divisionoccurred:\n",
    "            r = r+p.lt()\n",
    "            p = p-p.lt()\n",
    "\n",
    "    return q, r\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "970f651f-abae-4c15-af4d-07ff7ad9b062",
   "metadata": {},
   "source": [
    "# Implementando el ejemplo 5 del libro:\n",
    "Sean $f_1=xy-1$, $f_2=y^2-1$, con órden lexicográfico. Si dividimos $f=xy^2-x$ por $F=(f_1,f_2)$ obtenemos:\n",
    "$$\n",
    "xy^2-x=y \\cdot (xy-1) + 0\\cdot (y^2-1)+(-x+y)\n",
    "$$\n",
    "\n",
    "Si tomamos $F=(f_2,f_1)$, obtenemos:\n",
    "$$\n",
    "xy^2-x= x\\cdot (y^2-1) + 0 \\cdot(xy-1) + 0\n",
    "$$\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "f666ba7c-5576-4124-8a0e-42c3c555fa2a",
   "metadata": {},
   "outputs": [
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Caso 1:\n"
     ]
    },
    {
     "data": {
      "text/html": [
       "\\(x*y^2 - x = y (x*y - 1.00000000000000) + 0 (y^2 - 1.00000000000000) + (-x + y)\\)"
      ],
      "text/plain": [
       "\\(x*y^2 - x = y (x*y - 1.00000000000000) + 0 (y^2 - 1.00000000000000) + (-x + y)\\)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    },
    {
     "name": "stdout",
     "output_type": "stream",
     "text": [
      "Caso 2:\n"
     ]
    },
    {
     "data": {
      "text/html": [
       "\\(x*y^2 - x = x (y^2 - 1.00000000000000) + 0 (x*y - 1.00000000000000) + 0\\)"
      ],
      "text/plain": [
       "\\(x*y^2 - x = x (y^2 - 1.00000000000000) + 0 (x*y - 1.00000000000000) + 0\\)"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "R.<x,y> = PolynomialRing(RR, 2, \"xy\", order = \"lex\")\n",
    "\n",
    "f = x*y^2-x; f1=x*y-1; f2=y^2-1\n",
    "\n",
    "[q1,q2], r = div_poly([f1,f2],f)\n",
    "\n",
    "print(\"Caso 1:\")\n",
    "show(html(f\"${f} = {q1} ({f1}) + {q2} ({f2}) + ({r})$\"))\n",
    "\n",
    "\n",
    "print(\"Caso 2:\")\n",
    "f = x*y^2-x\n",
    "f1=x*y-1\n",
    "f2=y^2-1\n",
    "\n",
    "[q2,q1], r = div_poly([f2,f1],f)\n",
    "show(html(f\"${f} = {q2} ({f2}) + {q1} ({f1}) + {r}$\"))\n",
    "\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "57f1758b-790c-4686-8d43-5ef90f77bd69",
   "metadata": {},
   "source": [
    "# Aplicando el algoritmo con polinomios aleatorios"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 223,
   "id": "e4064892-5c56-4c3d-8689-20b4856b952c",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/html": [
       "\n",
       "\\[ \\begin{align*} f = &-0.651549359663330*x_2 + 0.880206541114476*x_3^2 + 0.0442587055060528*x_3 - 0.299907742749156*x_4^2 \\\\ \\sum_i q_i f_i +r = &-0.651549359663330*x_2 + 0.880206541114476*x_3^2 + 0.0442587055060528*x_3 - 0.299907742749156*x_4^2\\\\     f_1 = &-0.333298817031471*x_2 + 0.0951568793551716*x_3 \\quad \\quad q_1 = 1.95485050162002 \\\\     f_2 = &0.594003625357376*x_2 - 0.0638172336413463 \\quad \\quad q_2 = 0 \\\\     f_3 = &-0.510738189631297*x_2 - 0.404471935486321 \\quad \\quad q_3 = 0 \\\\     r = &0.880206541114476*x_3^2 - 0.141758767834000*x_3 - 0.299907742749156*x_4^2 \\end{align*}\\]\n"
      ],
      "text/plain": [
       "\n",
       "\\[ \\begin{align*} f = &-0.651549359663330*x_2 + 0.880206541114476*x_3^2 + 0.0442587055060528*x_3 - 0.299907742749156*x_4^2 \\\\ \\sum_i q_i f_i +r = &-0.651549359663330*x_2 + 0.880206541114476*x_3^2 + 0.0442587055060528*x_3 - 0.299907742749156*x_4^2\\\\     f_1 = &-0.333298817031471*x_2 + 0.0951568793551716*x_3 \\quad \\quad q_1 = 1.95485050162002 \\\\     f_2 = &0.594003625357376*x_2 - 0.0638172336413463 \\quad \\quad q_2 = 0 \\\\     f_3 = &-0.510738189631297*x_2 - 0.404471935486321 \\quad \\quad q_3 = 0 \\\\     r = &0.880206541114476*x_3^2 - 0.141758767834000*x_3 - 0.299907742749156*x_4^2 \\end{align*}\\]\n"
      ]
     },
     "metadata": {},
     "output_type": "display_data"
    }
   ],
   "source": [
    "degree=2\n",
    "n_var = 2\n",
    "# terms = binomial(degree+n_var, n_var)\n",
    "terms = 4\n",
    "\n",
    "R.<x_1,x_2,x_3, x_4> = PolynomialRing(RR, order=\"lex\")\n",
    "f = R.random_element(degree=degree, terms = terms)\n",
    "\n",
    "F = [R.random_element(degree=1, terms=2) for _ in range(3)]\n",
    "\n",
    "Q, r = div_poly(F, f, R = R)\n",
    "\n",
    "show(html(f\"\"\"\n",
    "$$\n",
    "\\\\begin{{align*}}\n",
    "f = &{f} \\\\\\\\\n",
    "\\\\sum_i q_i f_i +r = &{\n",
    "    sum([q*f for q,f in zip(Q,F)]) + r\n",
    "    }\\\\\\\\\n",
    "    f_1 = &{F[0]} \\\\quad \\\\quad q_1 = {Q[0]} \\\\\\\\\n",
    "    f_2 = &{F[1]} \\\\quad \\\\quad q_2 = {Q[1]} \\\\\\\\\n",
    "    f_3 = &{F[2]} \\\\quad \\\\quad q_3 = {Q[2]} \\\\\\\\\n",
    "    r = &{r}\n",
    "\\\\end{{align*}}\n",
    "$$\n",
    "\"\"\"))\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7c1de2c5-ec63-4856-8bce-0ae99551acd0",
   "metadata": {},
   "source": [
    "# Base escalonada"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 340,
   "id": "7e2ebc3e-347b-43e9-aa75-e88e6252c97e",
   "metadata": {},
   "outputs": [],
   "source": [
    "\n",
    "degree = 3\n",
    "dim = 3\n",
    "\n",
    "R.<x,y> = PolynomialRing(RR, order = \"lex\")\n",
    "\n",
    "V = VectorSpace(R.base_ring(), (degree+1)**2)\n",
    "\n",
    "polys = [R.random_element(degree = degree) for _ in range(dim)]\n",
    "\n",
    "vecs = [V([p.coefficient({x: i, y: j}) for i in range(degree+1) for j in range(degree+1)]) for p in polys]\n",
    "\n",
    "M = matrix(R.base_ring(), vecs)\n",
    "\n",
    "E=M.echelon_form()\n",
    "\n",
    "basis = []\n",
    "for row in E.rows():\n",
    "    p = sum(row[j]*x^(degree-i)*y^(degree-j) for i in range(degree+1) for j in range(degree+1))\n",
    "    basis.append(p)\n",
    "\n",
    "coords = [R.base_ring().random_element() for _ in range(dim)]\n",
    "\n",
    "coords\n",
    "\n",
    "poly = sum(basis[i]*coords[i] for i in range(dim))\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 341,
   "id": "476c79c6-c4b8-472f-86cf-05704c204df2",
   "metadata": {},
   "outputs": [
    {
     "data": {
      "text/plain": [
       "([0.909294046288622, -0.849447801567264, -0.590337792193350],\n",
       " [0.909294046288622, -0.849447801567264, -0.590337792193350])"
      ]
     },
     "execution_count": 341,
     "metadata": {},
     "output_type": "execute_result"
    }
   ],
   "source": [
    "Q, r = div_poly(basis, poly)\n",
    "\n",
    "Q, coords\n"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.7",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.13.8"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
